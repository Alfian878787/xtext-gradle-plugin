buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.xtend:xtend-gradle-plugin:0.4.7'
    classpath 'com.github.oehme.sobula:sobula:0.6.3'
  }
}

subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'java'

  sourceCompatibility = 1.6

  dependencies {
    compile gradleApi()
    compile 'com.google.guava:guava:18.0'
  }
}

configure(subprojects.findAll{p-> !p.name.contains('protocol')}) {
  apply plugin: 'org.xtend.xtend'

  dependencies {
    compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.8.4'
  }
}

configure(subprojects.findAll{p-> p.name.contains('plugin')}){
  apply plugin: 'java-gradle-plugin'
}

allprojects {
  apply plugin: 'com.github.oehme.sobula.plugin-release'

  group = "org.xtext"
  bintray.pkg.name = "$rootProject.group:$rootProject.name"

  contacts {
    "xtext@itemis.com" {
      moniker "The Xtext team"
      roles "owner"
      github "xtext"
    }
  }

  //TODO create bintray and central users
  bintray.user = "oehme"
  bintray.pkg.version.mavenCentralSync.user = "oehme"

  repositories {
    jcenter()
		maven {
			url 'https://oss.sonatype.org/content/repositories/snapshots'
		}
  }
}
