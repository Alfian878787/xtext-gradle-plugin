buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.xtext:xtext-gradle-plugin:2.0.2'
    classpath 'com.github.oehme.sobula:sobula:0.6.7'
    classpath 'com.netflix.nebula:nebula-project-plugin:4.0.1'
    classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
  }
}

allprojects {
  apply plugin: "io.spring.dependency-management"
  dependencyManagement {
    imports {
      mavenBom "org.eclipse.xtext:xtext-dev.bom:2.16.0-SNAPSHOT"
    }
  }
  apply plugin: 'com.github.oehme.sobula.plugin-release'

  group = "org.xtext"
  bintray.pkg.name = "org.xtext:xtext-gradle-plugin"

  contacts {
    "xtext.team@gmail.com" {
      moniker "The Xtext team"
      roles "owner"
      github "xtext"
    }
  }

  bintray.user = "xtext-team"
  bintray.pkg.version.mavenCentralSync.user = "xtext.team"

  repositories {
    maven {
      url 'https://projects.itemis.de/nexus/content/groups/xtext-bom'
    }
    jcenter()
  }
}

subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'java'
  

  sourceCompatibility = 1.8

  tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
  }

  dependencies {
    compile gradleApi()
    compile 'com.google.guava:guava:21.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.ow2.asm:asm:6.1.1'
  }

  tasks.withType(Test).all {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
  }
}

configure(subprojects.findAll{p-> !p.name.contains('protocol')}) {
  apply plugin: 'org.xtext.xtend'
}

apply from: "${rootDir}/gradle/xtend-bootstrap.gradle"

configure(subprojects.findAll{p-> p.name.contains('plugin')}){
  apply plugin: 'java-gradle-plugin'
  apply plugin: 'nebula.facet'

  jar {
    manifest {
      attributes("Implementation-Version": project.version)
    }
  }

  facets {
    integTest {
      parentSourceSet = 'test'
      testTaskName = 'minimumIntegrationTest'
    }
  }

  minimumIntegrationTest {
    dependsOn("install")
    systemProperty 'gradle.project.version', version
    systemProperty 'xtext.version', minimumXtextVersion
  }

  task latestIntegrationTest(type: Test) {
    classpath = minimumIntegrationTest.classpath
    testClassesDirs = minimumIntegrationTest.testClassesDirs
    dependsOn("install")
    systemProperty 'gradle.project.version', version
    systemProperty 'xtext.version', latestXtextVersion
    enabled = JavaVersion.current().java8Compatible
  }

  check.dependsOn(latestIntegrationTest)
}
